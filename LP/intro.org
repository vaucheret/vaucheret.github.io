#+TITLE: Lenguajes Declarativos
#+DATE: <2024-03-11 lun>
#+AUTHOR: Claudio Vaucheret
#+EMAIL: cv@fi.uncoma.edu.ar

#+REVEAL_INIT_OPTIONS:  transition:'slide' 
#+options: toc:1 num:nil

#+REVEAL_THEME: sky
#+REVEAL_HLEVEL: 2
#+reveal_root:  https://cdn.jsdelivr.net/npm/reveal.js

#+REVEAL_EXTRA_CSS: grids.css



* Lenguajes Declarativos

** Características

#+ATTR_REVEAL: :frag (roll-in)
 * Programa es Lógica + Control (Kowalski) 

 * X = X + 1 ¿que significa?

 * Semánticas equivalentes a la operacional

 * Programa es Algoritmo + Estructura de Datos (Wirth)

 * Debugging, Correctitud, Prueba de Programas

 * Análisis de Programas, Web Semantics

** Lenguajes
#+ATTR_REVEAL: :frag (roll-in)
- Relacional (Prolog)
  - Swi Prolog   [[http://www.swi-prolog.org/]]
  - Ciao Prolog  [[http://ciao-lang.org]]
- Funcional
  - scheme (lisp) https://racket-lang.org/
  - erlang (concurrency)
    - https://www.erlang.org/
    - https://adoptingerlang.org/
- Typed declarative languages
  - Haskell
    [[https://www.haskell.org/]]
  - Mercury
    [[https://mercurylang.org]]


* Prolog

** Programación en Lógica

#+ATTR_REVEAL: :frag (roll-in)
 * Cláusulas de Horn
 * Resolución SLD (basado en resolución de Robinson)
 * Lógica como lenguaje de programación 

** Ejemplo fibonacci
*** sucesion de fibonacci

En matemáticas, la sucesión de Fibonacci (a veces llamada erróneamente
serie de Fibonacci) es la siguiente sucesión infinita de números
naturales:

   $1,1,2,3,5,8,13,21,34,55,89,144,233,377,610,987,1597 \ldots$

La sucesión comienza con los números 1 y 1 y a partir de estos,
*cada término es la suma de los dos anteriores*, es la relación de
recurrencia que la define.

#+REVEAL: split

A los elementos de esta sucesión se les llama números de
Fibonacci. Esta sucesión fue descrita en Europa por Leonardo de Pisa,
matemático italiano del siglo XIII también conocido como
Fibonacci. Tiene numerosas aplicaciones en ciencias de la computación,
matemáticas y teoría de juegos. También aparece en configuraciones
biológicas, como por ejemplo en las ramas de los árboles, en la
disposición de las hojas en el tallo, en las flores de alcachofas y
girasoles, en las inflorescencias del brécol romanesco y en la
configuración de las piñas de las coníferas.

*** Conejos 

una forma de exponer la sucesión de fibonacci es presentando el
crecimiento de una población de conejos con las siguientes reglas:

- un conejo adulto, cada mes se reproduce a si mismo generando un
  conejo joven.
- un conejo joven se vuelve adulto en un mes.

| mes     | 0 | 1 | 2 | 3 | 4 | 5 |  6 |  7 |
|---------+---+---+---+---+---+---+----+----|
| adultos |   | 1 | 1 | 2 | 3 | 5 |  8 | 13 |
| jovenes | 1 |   | 1 | 1 | 2 | 3 |  5 |  8 |
|---------+---+---+---+---+---+---+----+----|
| total   | 1 | 1 | 2 | 3 | 5 | 8 | 13 | 21 |




*** programa 
#+BEGIN_SRC prolog
  :- module(fibonacci,[f/2]).

  f(0,1).
  f(1,1).
  f(N,R):-
      N >= 2,
      N1 is N - 1,
      N2 is N - 2,
      f(N1,F1),
      f(N2,F2),
      R is F1 + F2.
#+END_SRC


** Sintaxis

Términos:
 - Atomos
 - Números
 - Variables
 - Términos Complejos

los caracteres válidos son:
#+BEGIN_SRC prolog
  A , B  ...  Z
  a , b  ...  z
  + , - , * , / , < , > , = , : , . , & , ~
#+END_SRC



*** Atomos
 Un átomo es:
- Una cadena de caracteres de letras mayúsculas, minusculas, digitos
   y guión bajo, que comience con una letra minúscula.
   ejemplos:
   #+BEGIN_SRC prolog
     casa trabajo alberto eLLio x21
   #+END_SRC
- Una secuencia arbitraria de caracteres encerrado en comillas
   simples.
   ejemplos:
    #+BEGIN_SRC prolog
      'Alberto' 'Dolar21' 'EL Paso' '&^%si'
     #+END_SRC

#+REVEAL: split

- una cadena de solo caracteres especiales. 
   ejemplos
    #+BEGIN_SRC prolog
      @= ===> :- 
    #+END_SRC

*** Números

ejemplos
 #+BEGIN_SRC prolog
   -8 -1 34 23.98 10001 1697.877443  
#+END_SRC

*** Variables
 
Una variable es una cadena de caracteres de letras mayúsculas,
minúsculas, digitos, guión bajo, que comience con una letra mayúscula
o con un guión bajo. 
ejemplos:
 #+BEGIN_SRC prolog
   X Y Variable _etiqueta X_526 List HEAD
#+END_SRC

*** Términos Complejos

Un término se compone de un functor seguido de cero a N argumentos entre paréntesis y separados
por comas. Cada argumento es un término.

ejemplos:

#+BEGIN_SRC prolog
  fecha(12,octubre,2016)
  animal_peligroso('Leon')
  arbol(23,arbol(12,nil,nil),nil)
#+END_SRC

[[file:compoundterms.png]]


** Listas

#+ATTR_REVEAL: :frag (roll-in)
- .(1,.(2,.(3,.(4,[]))))   dos argumentos Cabeza y Cola ./2
- [1|[2|[3|[4|[]]]]]    notacion alternativa [|]
- [1,2|[3|[4|[]]]]  = [1,2,3|[4|[]]] = [1,2,3,4|[]] = [1,2,3,4] 
- Unificar [a,1,b,4,c] = [X,Y|Z] ---> X=a, Y=1, Z=[b,4,c]


** Ejemplo Autómata Finito

[[file:automata.png]]

- Ejemplos de cadenas aceptadas:
    
     aabbab, ab, aaab, babab

- Ejemplos de cadenas rechazadas
   
     aabb, aa, bb, ababa


*** Código 

El código en prolog que representa el autómata es

#+BEGIN_SRC prolog
final(s3).

trans(s1,a,s1).
trans(s1,a,s2).
trans(s1,b,s1).
trans(s2,b,s3).
trans(s3,b,s4).

silent(s2,s4).
silent(s3,s1).

#+END_SRC
[[file:automata.png]]

*** Código

el código que representa el reconocedor de las cadenas es

 #+BEGIN_SRC prolog
 
accepts_(State,[]):-
	final(State).

accepts_(State,[X|Rest]):-
	trans(State,X,State1),
	accepts_(State1,Rest).

accepts_(State,String):-
	silent(State,State1),
	accepts_(State1,String).

 #+END_SRC

*** Código

el código principal es 

#+BEGIN_SRC prolog

:- module(automata,[main/1],_).
:- use_module(library(iso_char)).

main([State,Chain]):-
	( accepts(State,Chain) -> display('aceptada '),nl
	;
	    display('rechazada '),nl
	).
main(_):- display('especificar estado y cadena '),nl.

accepts(State,String):-
	atom_chars(String, Chars),
	accepts_(State,Chars).

#+END_SRC

*** Arbol SLD

#+BEGIN_SRC prolog
accept(s1,[a,a,b]).
#+END_SRC
[[file:acceptaab.png]]

*** Arbol SLD

#+BEGIN_SRC prolog
accept(s1,[a,b]).
#+END_SRC
[[file:accepts1ab.png]]

*** Arbol SLD

#+BEGIN_SRC prolog
accept(s1,[b]).
#+END_SRC
[[file:accepts1b.png]]

*** Arbol SLD

#+BEGIN_SRC prolog
accept(s2,[b]).
#+END_SRC
[[file:accepts2b.png]]

*** Arbol SLD

#+BEGIN_SRC prolog
accept(s4,[b]).
#+END_SRC
[[file:accepts4b.png]]

*** $Tp^n$

#+REVEAL_HTML: <div style="font-size: 70%;">
    
$U = \{ a,b,s1,s2,s3,s4,[],[a],[b],[s1],\ldots,[a,a],[a,b],\ldots\}$

$Bp = \{final(a),final(b),\ldots, trans(a,a,a) , trans(a,b,a) , \ldots$

$accepts(a,a), accepts(a,[]) \ldots \}$

$Tp(\{\}) = \{final(s3),trans(s1,a,s1),trans(s1,a,s2),trans(s1,b,s1)$

$trans(s2,b,s3),trans(s3,b,s4),silent(s2,s4),silent(s3,s1)\}$ 

#+REVEAL: split

#+REVEAL_HTML: <div style="font-size: 70%;">

$Tp^1(\{\}) = Tp(\{\}) \cup \{accepts(s3,[])\}$

$Tp^2(\{\}) = Tp^1(\{\}) \cup \{accepts(s2,[b]\}$

$Tp^3(\{\}) = Tp^2(\{\}) \cup \{accepts(s1,[a,b])\}$

$Tp^4(\{\}) = Tp^3(\{\}) \cup \{accepts(s3,[a,b])$

$accepts(s1,[a,a,b]),accepts(s1,[b,a,b])\}$

$Tp^5(\{\}) = Tp^4(\{\}) \cup \{accepts(s2,[b,a,b])$

$accepts(s3,[a,a,b]),accepts(s3,[b,a,b]),accepts(s1,[a,a,a,b])$

$accepts(s1,[b,a,a,b]),accepts(s1,[a,b,a,b]),accepts(s1,[b,b,a,b])\}$
 
 


** Máquina de Turing
#+REVEAL_HTML: <div style="font-size: 60%;">   
Una máquina de Turing con una sola cinta puede definirse como una $7-tupla$:

$$M=(Q,\Sigma ,\Gamma ,s,b,F,\delta )$$

donde:

- $Q$ es un conjunto finito de estados.
- $\Sigma$ es un conjunto finito de símbolos distinto del espacio en blanco, denominado alfabeto de máquina o de entrada.
- $\Gamma$ es un conjunto finito de símbolos de cinta, denominado alfabeto de cinta
- $\Sigma \subseteq \Gamma$.
- $s\in Q$ es el estado inicial.
- $b\in \Gamma$ es un símbolo denominado blanco, y es el único símbolo que se puede repetir un número infinito de veces.
- $F\subseteq Q$ es el conjunto de estados finales de aceptación.
- $\delta :Q\times \Gamma \rightarrow Q\times \Gamma \times \{L,R\}} es una /función parcial/ denominada función de transición, donde 
$L$ es un movimiento a la izquierda y $R$ es el movimiento a la derecha.
 #+REVEAL_HTML: </div>

*** Código

#+BEGIN_SRC prolog
:- module(tur,_).


turing(Tape0, Tape) :-
       perform(q0, [], Ls, Tape0, Rs),
       reverse(Ls, Ls1),
       append(Ls1, Rs, Tape).

perform(qf, Ls, Ls, Rs, Rs) :- !.

perform(Q0, Ls0, Ls, Rs0, Rs) :-
       symbol(Rs0, Sym, RsRest),
       once(rule(Q0, Sym, Q1, NewSym, Action)),
       action(Action, Ls0, Ls1, [NewSym|RsRest], Rs1),
       perform(Q1, Ls1, Ls, Rs1, Rs).

symbol([], b, []).
symbol([Sym|Rs], Sym, Rs).

action(left, Ls0, Ls, Rs0, Rs) :- left(Ls0, Ls, Rs0, Rs).
action(stay, Ls, Ls, Rs, Rs).
action(right, Ls0, [Sym|Ls0], [Sym|Rs], Rs).

left([], [], Rs0, [b|Rs0]).
left([L|Ls], Ls, Rs, [L|Rs]).

rule(q0, 1, q0, 1, right).
rule(q0, b, qf, 1, stay).


%% rule(q0, 1, q0,  1,right).
%% rule(q0, 0, q0,  0,right).
%% rule(q0,b,q1,c,left).

%% rule(q1, 0, q1,  0,left ).
%% rule(q1, 1, q1,  1,left ).
%% rule(q1,b,q2,b,right).

%% rule(q2, 0, q3,y,right).
%% rule(q2, 1, q7,x,right).
%% rule(q2,c, q11,c,left).


%% rule(q3, 0, q3,  0,right).
%% rule(q3, 1, q3,  1,right).
%% rule(q3,c,q4,c,right).

%% rule(q4,  0,q4,  0,right).
%% rule(q4,  1,q4,  1,right).
%% rule(q4,b,q5,  0,left ).

%% rule(q5,  0,q5,  0,left ).
%% rule(q5,  1,q5,  1,left ).
%% rule(q5,c,q6,c,left ).

%% rule(q6,  0,q6,  0,left ).
%% rule(q6,  1,q6,  1,left ).
%% rule(q6,y,q2,y,right).

%% rule(q7, 0, q7,  0,right).
%% rule(q7, 1, q7,  1,right).
%% rule(q7,c,q8,c,right).

%% rule(q8,  0,q8,  0,right).
%% rule(q8,  1,q8,  1,right).
%% rule(q8,b,q9,  1,left ).

%% rule(q9,  0,q9,  0,left ).
%% rule(q9,  1,q9,  1,left ).
%% rule(q9,c,q10,c,left ).

%% rule(q10,  0,q10,  0,left ).
%% rule(q10,  1,q10,  1,left ).
%% rule(q10,x,q2,x,right).

%% rule(q11,x,q11,1,left).
%% rule(q11,y,q11,0,left).
%% rule(q11,b,qf,b,stay).
#+END_SRC
